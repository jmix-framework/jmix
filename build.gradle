buildscript {
    repositories {
        if (rootProject.hasProperty('jmixRepoUrl')) {
            if (rootProject.hasProperty('jmixUseLocalMavenRepository')) {
                mavenLocal()
            }
            maven {
                url rootProject['jmixRepoUrl']

                if (rootProject.hasProperty('jmixRepoUser') && rootProject.hasProperty('jmixRepoPassword')) {
                    credentials {
                        username rootProject['jmixRepoUser']
                        password rootProject['jmixRepoPassword']
                    }
                }
            }
            gradlePluginPortal()
        } else {
            mavenLocal()
            maven {
                url "https://nexus.jmix.io/repository/public"
            }
            gradlePluginPortal()
        }
    }
    dependencies {
        classpath("io.jmix.build:jmix-build:${rootProject.findProperty('buildPluginVersion') ?: version}")
        classpath("io.jmix.gradle:jmix-gradle-plugin:${rootProject.findProperty('jmixPluginVersion') ?: version}")
    }
}

plugins {
    id "org.sonarqube" version "6.3.1.5724"
    id "org.cyclonedx.bom" version "3.0.2"
}

sonarqube {
    properties {
        property "sonar.exclusions", """
        **/io/jmix/data/impl/jpql/antlr2/**
        """
    }
}

configure(subprojects.findAll { it.name != 'bom' }) {
    apply(plugin: 'io.jmix.build')
}
