/*
 * Copyright 2019 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

group = 'io.jmix.build'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation localGroovy()
    implementation 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.9'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

gradlePlugin {
    website = 'http://jmix.io'
    vcsUrl = 'https://github.com/jmix-framework/jmix'

    plugins {
        jmixPlugin {
            id = 'io.jmix.build'
            displayName = 'Jmix internal build support'
            description = 'Jmix internal build support'
            tags.set(['jmix', 'spring'])
            implementationClass = 'io.jmix.build.JmixBuildPlugin'
        }
    }
}

publishing {
    if (rootProject.hasProperty('jmixUploadUrl')) {
        repositories {
            maven {
                url = rootProject['jmixUploadUrl']
                credentials {
                    username rootProject['jmixUploadUser']
                    password rootProject['jmixUploadPassword']
                }
                allowInsecureProtocol = true
            }
        }
    }
}
