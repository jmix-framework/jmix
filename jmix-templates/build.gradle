/*
 * Copyright 2020 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'base'
    id 'maven-publish'
}

group = 'io.jmix.templates.studio'

task copyContent(type: Copy) {
    from('content') {
        exclude '**/gradle/wrapper/**'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [bomVersion: version])
    }
    from('content') {
        include '**/gradle/wrapper/**'
    }
    into "$buildDir/content"
}

task contentJar(type: Jar, dependsOn: copyContent) {
    from("$buildDir/content")
    manifest {
        attributes('Studio-Version-Range': '0-1') // perhaps we should keep it either here OR in pom (see below)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'jmix-studio-templates'
            artifact(contentJar)
            pom {
                properties = ['studioVersionRange': '0-1']
            }
        }
    }
    if (rootProject.hasProperty('jmixUploadUrl')) {
        repositories {
            maven {
                url = rootProject['jmixUploadUrl']
                credentials {
                    username rootProject['jmixUploadUser']
                    password rootProject['jmixUploadPassword']
                }
                allowInsecureProtocol = true
            }
        }
    }
}